plugins {
    id "java"
}

repositories {
    mavenCentral()
}

group = "org.embulk"
version = "0.10.45-SNAPSHOT"

def subprojectNamesOfCoreArtifacts = [
    "embulk-spi",
    "embulk-core",
    "embulk-deps",
    "embulk-junit4",
]

def embeddedPlugins = [
    [ group: "org.embulk", name: "embulk-decoder-bzip2", version: "0.10.40" ],
    [ group: "org.embulk", name: "embulk-decoder-gzip", version: "0.10.40" ],
    [ group: "org.embulk", name: "embulk-encoder-bzip2", version: "0.10.40" ],
    [ group: "org.embulk", name: "embulk-encoder-gzip", version: "0.10.40" ],
    [ group: "org.embulk", name: "embulk-filter-remove_columns", version: "0.10.40" ],
    [ group: "org.embulk", name: "embulk-filter-rename", version: "0.10.40" ],
    [ group: "org.embulk", name: "embulk-formatter-csv", version: "0.10.40" ],
    [ group: "org.embulk", name: "embulk-guess-bzip2", version: "0.10.40" ],
    [ group: "org.embulk", name: "embulk-guess-csv", version: "0.10.40" ],
    [ group: "org.embulk", name: "embulk-guess-csv_all_strings", version: "0.10.40" ],
    [ group: "org.embulk", name: "embulk-guess-gzip", version: "0.10.40" ],
    [ group: "org.embulk", name: "embulk-guess-json", version: "0.10.40" ],
    [ group: "org.embulk", name: "embulk-input-config", version: "0.10.40" ],
    [ group: "org.embulk", name: "embulk-input-file", version: "0.10.40" ],
    [ group: "org.embulk", name: "embulk-output-file", version: "0.10.40" ],
    [ group: "org.embulk", name: "embulk-output-null", version: "0.10.40" ],
    [ group: "org.embulk", name: "embulk-output-stdout", version: "0.10.40" ],
    [ group: "org.embulk", name: "embulk-parser-csv", version: "0.10.40" ],
    [ group: "org.embulk", name: "embulk-parser-json", version: "0.10.40" ],
]

configurations {
    compileClasspath.resolutionStrategy.activateDependencyLocking()
    runtimeClasspath.resolutionStrategy.activateDependencyLocking()

    depsEmbedded {
        resolutionStrategy.activateDependencyLocking()
    }

    pluginEmbedded {
        resolutionStrategy.activateDependencyLocking()
    }

    embedded {
        extendsFrom(depsEmbedded, pluginEmbedded)
    }
}

dependencies {
    implementation project(':embulk-core')

    // Logback 1.4.x seems to be the latest as of Feb, 2023. But actually, their version strategy is:
    // * Logback 1.3.x for Java 8 and Java EE (javax.*)
    // * Logback 1.4.x for Java 11 and Jakarta EE (jakarta.*)
    // https://logback.qos.ch/dependencies.html
    implementation "ch.qos.logback:logback-classic:1.3.5"

    depsEmbedded project(":embulk-deps")

    embeddedPlugins.each { pluginArtifact ->
        pluginEmbedded pluginArtifact
    }
}

def listEmbeddedDependencies = { rootModuleName, prefix ->
    def firstArtifact = []  // The artifact of rootModuleName -- the list should contain only one.
    def followingArtifacts = []  // Artifacts depended from the artifact of rootModuleName.

    configurations.embedded.resolvedConfiguration.firstLevelModuleDependencies.each { firstLevelDependency ->
        if (firstLevelDependency.moduleName == rootModuleName) {
            firstLevelDependency.allModuleArtifacts.each { requiredArtifact ->
                if (requiredArtifact.name == rootModuleName) {
                    firstArtifact.add(prefix + requiredArtifact.file.name)
                } else {
                    followingArtifacts.add(prefix + requiredArtifact.file.name)
                }
            }
        }
    }

    if (firstArtifact.isEmpty()) {
        if (followingArtifacts.isEmpty()) {
            throw new GradleException('Failed to collect libraries to embedded: no dependencies found')
        } else {
            throw new GradleException('Failed to collect libraries to embedded: specified root module is not found')
        }
    } else if (firstArtifact.size() > 1) {
        throw new GradleException('Failed to collect libraries to embedded: multiple root modules are found')
    }

    // It guarantees that the artifact of rootModuleName comes first.
    return String.join(' ', firstArtifact + followingArtifacts)
}

// Standard "jar" task to build a JAR with dependency JAR resources embedded.
jar {
    dependsOn ":embulk-spi:jar",
              ":embulk-core:jar",
              ":embulk-deps:jar"

    // Expands all dependencies including "embulk-core"
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    // Includes COPYING.
    from "${rootProject.projectDir}/COPYING"

    // Includes all hidden dependency librarires under "embedded" into "lib/".
    into("/lib") {
        from configurations.embedded
    }

    // NOTE: This 'Implementation-Version' in the manifest is referred to provide the Embulk version at runtime.
    // See also: embulk-core/src/main/java/org/embulk/EmbulkVersion.java
    manifest {
        attributes 'Implementation-Title': 'embulk',
                   'Implementation-Version': project.version,
                   'Implementation-Vendor-Id': project.group,
                   'Specification-Title': 'embulk',
                   'Specification-Version': project.version,
                   'Embulk-Resource-Class-Path': listEmbeddedDependencies('embulk-deps', '/lib/'),
                   "Main-Class": "org.embulk.cli.Main",
                   "Embulk-Plugins": String.join(" ", embeddedPlugins.collect { artifact -> artifact["name"] }),

                   // "json" should be registered before "csv".
                   // charset and newline guess plugins are loaded and invoked by CsvGuessPlugin
                   "Embulk-Default-Guess-Plugins": "gzip,bzip2,json,csv"

        def pluginAttributes = embeddedPlugins.collectEntries { artifact ->
            [ ("Embulk-Plugin-${artifact['name']}".toString()): listEmbeddedDependencies(artifact["name"], "/lib/") ]
        }
        attributes(pluginAttributes)
    }
}

task executableJar(dependsOn: "jar") {
    ext.destinationDir = file("${buildDir}/executable")
    doFirst {
        destinationDir.mkdirs()
    }
    ext.destination = file("${destinationDir}/embulk-${project.version}.jar")
    doLast {
        destination.write("")
        destination.append("\n: <<END_OF_EMBULK_SELFRUN_BATCH_PART\r\n")
        destination.append(file("embulk-core/src/main/bat/selfrun.bat").readBytes())
        destination.append("\r\nEND_OF_EMBULK_SELFRUN_BATCH_PART\r\n\n")
        destination.append(file("embulk-core/src/main/sh/selfrun.sh").readBytes())
        destination.append(jar.outputs.files.singleFile.readBytes())
        destination.setExecutable(true)
    }
}

task releaseCheck {
    doFirst {
        if (rootProject.version.endsWith("-SNAPSHOT")) {
            throw new GradleException("Not for release. The version in build.gradle is SNAPSHOT: ${rootProject.version}")
        }
        // TODO: Revisit if we would check the format of tag annotation.
    }
    doLast {
        println "Ready. Run 'release' task."
    }
}

task release {
    dependsOn "releaseCheck"
    dependsOn "executableJar"
    subprojectNamesOfCoreArtifacts.each { subprojectName ->
        dependsOn ":${subprojectName}:publishMavenPublicationToMavenCentralRepository"
        tasks.findByPath(":${subprojectName}:publishMavenPublicationToMavenCentralRepository").mustRunAfter(":releaseCheck")
    }
    dependsOn "embulk-ruby:gemPush"
}
